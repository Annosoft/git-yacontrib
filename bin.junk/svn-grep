#! /usr/bin/perl -T

use strict;
use warnings;
$ENV{PATH} = '/bin:/usr/bin';

# Extract dirs list from the end of @ARGV.  This isn't easy to do
# properly without duplicating grep's option parsing, so we'll cheat
my @dirs;
while (@ARGV && ($ARGV[-1] eq '--' || -d $ARGV[-1])) {
    my $last = pop @ARGV;
    push @dirs, $last if $last ne '--';
}

my @grep_opts = @ARGV;

my @cmd = ("find", @dirs,
	   qw{ -type d ( -name .svn -o -name DEVINST -o -name blib ) -prune -o -type f -print0 });
open my $findfh, "-|", @cmd or die "Can't pipe from '@cmd': $!";
my @files = split /\x00/, join "", <$findfh>;
close $findfh;

@cmd = (grep => @grep_opts, @files);
open my $grepfh, "-|", @cmd or die "Can't pipe from '@cmd': $!";
while (<$grepfh>) {
    print;
}
close $grepfh;
exit $? >> 8;

__END__

#! /bin/sh

# Doesn't exclude all of .svn/
# Excludes duplicate copy of working files (svn-base)
# *.svn-work would only show with negative grep query

exec grep -r --exclude entries --exclude '*.svn-base' --exclude '*.svn-work' ${1+"$@"}
